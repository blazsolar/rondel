/*
 *    Copyright 2016 Blaž Šolar
 *
 *    Licensed under the Apache License, Version 2.0 (the "License");
 *    you may not use this file except in compliance with the License.
 *    You may obtain a copy of the License at
 *
 *        http://www.apache.org/licenses/LICENSE-2.0
 *
 *    Unless required by applicable law or agreed to in writing, software
 *    distributed under the License is distributed on an "AS IS" BASIS,
 *    WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *    See the License for the specific language governing permissions and
 *    limitations under the License.
 */

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.ltgt.gradle:gradle-apt-plugin:0.5"
    }
}

group 'solar.blaz'
version '0.1.0'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: "jacoco"
apply plugin: 'idea'
apply plugin: "net.ltgt.apt"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "s3://kamino-maven/lib-release"
        credentials(AwsCredentials) {
            accessKey kaminoMavenAccessKey
            secretKey kaminoMavenSecretKey
        }
    }
}

dependencies {
    compile project(':rondel')
    compile 'com.google.dagger:dagger:2.1'
    compile 'com.squareup:javapoet:1.5.1'
    compile 'com.google.guava:guava:19.0'
    compile 'com.google.auto:auto-common:0.6'

    compileOnly'com.google.android:android:4.1.1.4'
    
    apt 'com.google.dagger:dagger-compiler:2.1'

    testCompile 'junit:junit:4.11'
    testCompile 'com.google.truth:truth:0.28'
    testCompile 'com.google.testing.compile:compile-testing:0.8'
    testCompile files(org.gradle.internal.jvm.Jvm.current().getToolsJar())
    testCompile 'com.google.dagger:dagger-compiler:2.1'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*_MembersInjector*',
                    '**/*_Provide*Factory*',
                    '**/*Factory*'
            ])
        })
    }
}

test {
    jacoco {
        excludes = [
                '**.*_MembersInjector*',
                '**.*_Provide*Factory*',
                '**.*Factory*'
        ]
    }
}



publishing {
    repositories {
        maven {
            url "s3://kamino-maven.s3.eu-central-1.amazonaws.com/lib-snapshot"
            credentials(AwsCredentials) {
                accessKey kaminoMavenAccessKey
                secretKey kaminoMavenSecretKey
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}